plugins {
    alias libs.plugins.android.application
    alias libs.plugins.kotlin
    id 'kotlin-kapt'
    alias libs.plugins.detekt
    alias libs.plugins.hilt
    alias libs.plugins.google.services
    alias libs.plugins.crashlytics
}

apply from: "$rootDir/gradle/coverage.gradle"

android {
    namespace 'com.santimattius.basic.skeleton'
    compileSdkVersion Integer.valueOf(target_sdk_version)

    defaultConfig {
        applicationId application_id
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode Integer.valueOf(version_code)
        versionName version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
                jacoco.excludes = ['jdk.internal.*']
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.4'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

detekt {
    config = files("${project.rootDir}/config/detekt/detekt.yml")
    baseline = file("$rootDir/detekt-baseline.xml")
    autoCorrect = true
}

dependencies {

    implementation libs.core.ktx
    implementation libs.lifecycle.runtime.ktx
    implementation libs.lifecycle.viewmodel.compose
    implementation libs.lifecycle.runtime.compose

    implementation libs.bundles.compose
    debugImplementation libs.bundles.compose.debug

    implementation libs.bundles.coroutine
    implementation libs.bundles.retrofit
    implementation libs.gson.core

    implementation libs.hilt.android
    implementation libs.hilt.navigation
    kapt libs.hilt.compiler

    implementation libs.coil.core

    implementation platform('com.google.firebase:firebase-bom:31.3.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    testImplementation libs.junit

    androidTestImplementation libs.compose.ui.test.junit
    androidTestImplementation libs.test.ext
    androidTestImplementation libs.test.espresso

}

kapt {
    correctErrorTypes true
}